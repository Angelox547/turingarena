protocol_declaration =
  'protocol' [name:identifier] '{'
    steps:protocol_nodes
  '}'
  ;

protocol_nodes =
  {protocol_node}*
  ;

protocol_node =
  | input_step
  | output_step
  | call_step
  | for_node
  | switch_node
  ;

input_step::InputStep =
  'input' ','.{ items+:lvalue }* ';'
  ;

output_step::OutputStep =
  'output' ','.{ items+:lvalue }* ';'
  ;

call_step::CallStep =
  'call' [ return_value:lvalue '='  ] function_name:identifier '(' ','.{ parameters+:lvalue }* ')' ';'
  ;

for_node::ForNode =
  'for' '(' index:identifier ':' range:range ')' '{'
    steps:protocol_nodes
  '}'
  ;

switch_node::SwitchNode =
  'switch' '(' index:lvalue ')' '{'
    cases:{ switch_case }*
  '}'
  ;

switch_case::SwitchCase =
  'case' '(' expression:expr ')' '{'
    steps:protocol_nodes
  '}'
  ;

