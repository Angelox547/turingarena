@@comments :: /\/\*(.|\n|\r)*\*\//
@@eol_comments :: /\/\/.*$/

start = 
  {
    | interfaces+:interface
    | variables+:global_variable_declaration
    | functions+:function_declaration
  }* $ ;

identifier = /[a-zA-Z_][0-9a-zA-Z_]*/ ;

interface =
  'interface' name:identifier '{' declarations:{declaration}* '}'
  ;

declaration =
  | function_declaration
  | global_variable_declaration
  ;

global_variable_declaration =
  inout:(inout|`inout`) variable:variable ';'
  ;

function_declaration =
  callback:['callback'] return_type:type name:identifier '(' parameters:','.{parameter}* ')' ';'
  ;

parameter =
  variable:variable
  ;

variable =
  type:type name:identifier {'[' array_dimensions+:array_dimension ']'}*
  ;

array_dimension =
  | constant:/[1-9][0-9]*/
  | variable_reference:identifier
  ;

type =
  'int'
  ;

inout = 'in' | 'out' | 'inout' ;
